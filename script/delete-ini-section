#!perl

# DATE
# VERSION

use 5.010;
use strict;
use warnings;
no warnings 'once';

use App::INIUtils;
use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{delete_ini_section} = {
    v => 1.1,
    summary => 'Delete a section (and its contents) from '.
        'INI configuration file',
    args => {
        %App::INIUtils::args_common,
        %App::INIUtils::arg_inplace,
        section => {
            summary => 'Section name',
            schema  => 'str*',
            pos     => 1,
        },
        section_pattern => {
            summary => 'Regex to match section name',
            schema => 'str*',
        },
        all => {
            summary => 'Delete all occurrences instead of just the first',
            schema => [bool => is=>1],
        },
    },
    args_rels => {
        req_one => [qw/section section_pattern/],
    },
};
sub delete_ini_section {
    my %args = @_;
    App::INIUtils::_check_inplace(\%args);

    my $parser = App::INIUtils::_get_cii_parser(\%args);

    my $doc = $parser->read_string($args{ini});

    my @sections;
    if (defined $args{section}) {
        @sections = ($args{section});
    } else {
        my $re = qr/$args{section_pattern}/;
        @sections = grep { /$re/ } $doc->list_sections(unique => 1);
    }
    for (@sections) {
        $doc->delete_section(
            {
                all     => $args{all},
            },
            $_,
        );
    }

    App::INIUtils::_return_mod_result(\%args, $doc);
}

Perinci::CmdLine::Any->new(
    url => '/main/delete_ini_section',
    read_config => 0,
    read_env => 0,
)->run;

# ABSTRACT:
# PODNAME:

=head1 SYNOPSIS

Basic usage:

 % delete-ini-section /path/to/file.ini section
